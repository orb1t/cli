apply plugin: 'nexus'

def optionalDeps = []
ext {
    optional = { optionalDeps << it; it }
}

modifyPom {
    project {
        name 'CLI'
        description 'A lightweight Command Line Interface for Java'
        url 'http://vtence.com/cli'
        packaging 'jar'
        licenses {
            license {
                name 'MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/testinfected/cli'
            connection 'scm:git:https://github.com/testinfected/cli.git'
            developerConnection 'scm:git@github.com:testinfected/cli.git'
        }
        developers {
            developer {
                id 'testinfected'
                name 'Vincent TencÃ©'
            }
        }

        optionalDeps.each { dep ->
            dependencies.find { it.artifactId == dep.name }.optional = true
        }

        // no need to publish test dependencies
        dependencies.removeAll { it.scope == "test" }
    }
}

nexus {
    attachTests = true
}